library ieee;
use ieee.std_logic_1164.all;

entity scancode_display is
    port (
        sys_clk    : in  std_logic;
        reset      : in  std_logic;                     -- active high
        char_code  : in  std_logic_vector(7 downto 0);  -- from keyboard.vhd
        char_ready : in  std_logic;                     -- 1-cycle pulse
        seg0       : out std_logic_vector(6 downto 0);  -- newest byte, low nibble
        seg1       : out std_logic_vector(6 downto 0);  -- newest byte, high nibble
        seg2       : out std_logic_vector(6 downto 0);  -- previous byte, low
        seg3       : out std_logic_vector(6 downto 0);  -- previous byte, high
        seg4       : out std_logic_vector(6 downto 0);  -- third byte, low
        seg5       : out std_logic_vector(6 downto 0)   -- third byte, high
    );
end scancode_display;

architecture rtl of scancode_display is

    -- c1 = newest scancode, c3 = oldest
    signal c1, c2, c3 : std_logic_vector(7 downto 0);

begin

    --------------------------------------------------------------------
    -- Shift register for last 3 bytes
    --------------------------------------------------------------------
    process(sys_clk, reset)
    begin
        if reset = '1' then
            c1 <= (others => '0');
            c2 <= (others => '0');
            c3 <= (others => '0');
        elsif rising_edge(sys_clk) then
            if char_ready = '1' then
                -- shift: c1 -> c2 -> c3, new char_code into c1
                c3 <= c2;
                c2 <= c1;
                c1 <= char_code;
            end if;
        end if;
    end process;

    --------------------------------------------------------------------
    -- Hex nibble to 7-seg display
    -- seg0/1: newest byte
    -- seg2/3: previous byte
    -- seg4/5: third byte
    --------------------------------------------------------------------

    hex0 : entity work.hex_to_7seg
        port map (
            input  => c1(3 downto 0),   -- low nibble
            output => seg0
        );

    hex1 : entity work.hex_to_7seg
        port map (
            input  => c1(7 downto 4),   -- high nibble
            output => seg1
        );

    hex2 : entity work.hex_to_7seg
        port map (
            input  => c2(3 downto 0),
            output => seg2
        );

    hex3 : entity work.hex_to_7seg
        port map (
            input  => c2(7 downto 4),
            output => seg3
        );

    hex4 : entity work.hex_to_7seg
        port map (
            input  => c3(3 downto 0),
            output => seg4
        );

    hex5 : entity work.hex_to_7seg
        port map (
            input  => c3(7 downto 4),
            output => seg5
        );

end architecture rtl;